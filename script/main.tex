\documentclass{article}

\usepackage{xcolor}
\usepackage{listings}
\definecolor{codegreen}{rgb}{0,0.6,0}
\definecolor{codegray}{rgb}{0.5,0.5,0.5}
\definecolor{codepurple}{rgb}{0.58,0,0.82}
\definecolor{backcolour}{rgb}{0.95,0.95,0.92}

\lstdefinestyle{language_style}{
    % backgroundcolor=\color{backcolour},   
    commentstyle=\color{codegreen},
    keywordstyle=\color{magenta},
    numberstyle=\tiny\color{codegray},
    stringstyle=\color{codepurple},
    basicstyle=\ttfamily\footnotesize,
    breakatwhitespace=false,         
    breaklines=true,                 
    captionpos=b,                    
    keepspaces=true,                 
    numbers=left,                    
    numbersep=5pt,                  
    showspaces=false,                
    showstringspaces=false,
    showtabs=false,                  
    tabsize=2
}
\lstset{style=language_style}
\lstnewenvironment{bazel}{\lstset{language=python}}{}

\title{Bazel Presentation Notes}
\author{Brae Webb}
\date{August, 2020}

\begin{document}
    \maketitle


\section{Basic Bazel: Java}
\begin{enumerate}
    \item Open java/tree\_serializer and explain SerializeTree
    \item Write a BUILD file for java\_binary
\begin{bazel}
java_binary(
    name = "tree_serializer",
    srcs = [
        "SerializeTree.java",
    ],
    main_class = "SerializeTree"
)
\end{bazel}
    \begin{enumerate}
        \item the rule is java\_binary
        \item the specific instructions here create a target using java\_binary
        \item every target needs a name
        \item other parameters vary and can be found in the docs
    \end{enumerate}
    \item Explain 3 modes of execution (slide)
    \item bazel build tree\_serializer in same directory
    \item bazel build //example/java/tree\_serializer:tree\_serializer from anywhere
    \item bazel build //example/java/tree\_serializer as shorthand when target
    name equals directory name
    \item note that it logs artifacts
    \item artifacts are stored in bazel-bin
    \item break SerializeTree into InvalidFormat, Node and StringProcessor
    \item rewrite BUILD to include java\_library
\begin{bazel}
java_library(
    name = "node_lib",
    srcs = [
        "Node.java"
    ],
)

java_binary(
    name = "tree_serializer",
    srcs = glob([
        "*.java",
    ]),
    deps = [":node_lib"],
    main_class = "SerializeTree"
)    
\end{bazel}
    \item explain the glob function allows wildcard
    \item explain that : means that it is a reference to another target
\end{enumerate}

\section{Basic Bazel: Python}
\begin{enumerate}
    \item write a solution to the message decoder
    \item write a py\_binary for enumerator.py
    \item write test\_enumerator.py
    \item write a test for py\_test for test\_enumerator.py
\begin{bazel}
py_binary(
    name = "decode_enumerator",
    srcs = ["enumerator.py"],
    main = "enumerator.py",
)

py_test(
    name = "decode_enumerator_test",
    srcs = ["test_enumerator.py"],
    deps = [":decode_enumerator"],
    main = "test_enumerator.py",
)
\end{bazel}
    \item run with bazel test
\end{enumerate}

\section{External Rules}
\begin{enumerate}
    \item Go to google
    \item Lookup go bazel rules
    \item Update WORKSPACE
\begin{bazel}
load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_archive(
    name = "io_bazel_rules_go",
    sha256 = "2697f6bc7c529ee5e6a2d9799870b9ec9eaeb3ee7d70ed50b87a2c2c97e13d9e",
    urls = [
        "https://mirror.bazel.build/github.com/bazelbuild/rules_go/releases/download/v0.23.8/rules_go-v0.23.8.tar.gz",
        "https://github.com/bazelbuild/rules_go/releases/download/v0.23.8/rules_go-v0.23.8.tar.gz",
    ],
)

load("@io_bazel_rules_go//go:deps.bzl", "go_register_toolchains", "go_rules_dependencies")

go_rules_dependencies()

go_register_toolchains()
\end{bazel}
    \item Create BUILD
\begin{bazel}
load("@io_bazel_rules_go//go:def.bzl", "go_binary")

go_binary(
    name = "server",
    srcs = ["main.go"],
)
\end{bazel}
\end{enumerate}


\section{Querying Bazel}
\begin{enumerate}
    \item Run a simple query \texttt{bazel query "//..."}
    \item Run a simple query \texttt{bazel query "deps(//...)"}
    \item Run a simple query \texttt{bazel query "deps(//example/python/decode\_enumerator)"}
    \item \texttt{bazel query "deps(//example/...)" --output graph --notool\_deps | dot -Tpng > graph.png \&\& open graph.png}
    \item \texttt{bazel query "kind(py\_test, //example/...)"}
\end{enumerate}


\section{Extending Bazel: SML}
\begin{enumerate}
    \item go find an standard ML compiler (mlton)
    \item download the linux version from source forge
    \item extract the compiler

\texttt{./bin/mlton ~/work/bazel-presentation/example/ml/regex.sml}

\texttt{ls ~/work/bazel-presentation/example/ml}

\texttt{~/work/bazel-presentation/example/ml/regex}

    \item create a new \texttt{bazel\_rules/ml.bzl}
    \item write a new repository rule to download the compiler
\begin{bazel}
def _sml_compiler_repository(ctx):
    ctx.download_and_extract()

sml_compiler = repository_rule(
    implementation = _sml_compiler_repository,
)
\end{bazel}

    \item create a dict for with the required information
\begin{bazel}
_ml_compilers = {
    "Release20200722": {
        "linux": {
            "sha": "2829b0d138a6664022c14b0814aae82f68fba3f8443dd454737697ad6cce4b92",
            "url": "https://sourceforge.net/projects/mlton/files/mlton/20200722/mlton-20200722-1.amd64-linux.tgz",
            "prefix": "mlton-20200722-1.amd64-linux",
        }
    }
}
\end{bazel}
    \item hash the file with \texttt{sha256sum mlton-20200722-1.amd64-linux.tgz}
    \item use those parameters in the rule
\begin{bazel}
def _sml_compiler_repository(ctx):
    release = ctx.attr.release
    if release not in _ml_compilers:
        fail("Unknown release version: " + release)

    platform = ctx.os.name
    if platform not in _ml_compilers[release]:
        fail("Unsupported operating system: " + platform)
    
    remote = _ml_compilers[release][platform]

    ctx.download_and_extract(
        url = [remote["url"]],
        sha256 = remote["sha"],
        stripPrefix = remote["prefix"],
    )

sml_compiler = repository_rule(
    attrs = {
        "release": attr.string(mandatory = True),
    },
    implementation = _sml_compiler_repository,
)
\end{bazel}
    \item call the new repository rule in the workspace
\begin{bazel}
# ML Compiler
load("//example/bazel_rules:ml.bzl", "sml_compiler")

sml_compiler(
    name = "ml_compiler",
    release = "Release20200722",
)
\end{bazel}
    \item go fetch the repository \texttt{bazel fetch @ml\_compiler//...}
    \item stub a new rule for ml\_binary
\begin{bazel}
def _ml_binary(ctx):
    binary = ctx.actions.declare_file(ctx.label.name)

    return [DefaultInfo(executable = binary)]


ml_binary = rule(
    implementation = _ml_binary,
    attrs = {
        "main": attr.label(mandatory = True, allow_single_file = True),
        "srcs": attr.label_list(allow_files = True),
    },
    executable = True,
)
\end{bazel}
    \item create a rule in ml/BUILD
\begin{bazel}
load("//example/bazel_rules:ml.bzl", "ml_binary")

ml_binary(
    name = "regex",
    main = "regex.sml",
)
\end{bazel}
    \item run and have fail
    \item create an action to run the compiler
\begin{bazel}
def _ml_binary(ctx):
    binary = ctx.actions.declare_file(ctx.label.name)

    ctx.actions.run(
        executable = ctx.executable._compiler,
        use_default_shell_env = True,
        arguments = [
            "-output",
            binary.path,
            ctx.file.main.path,            
        ],
        inputs = depset(
            direct = ctx.files.main + ctx.files.srcs,
        ),
        outputs = [binary],
        tools = [ctx.executable._compiler],
    )

    return [DefaultInfo(executable = binary)]


ml_binary = rule(
    implementation = _ml_binary,
    attrs = {
        "main": attr.label(mandatory = True, allow_single_file = True),
        "srcs": attr.label_list(allow_files = True),
        "_compiler": attr.label(
            default = "@ml_compiler//:bin/mlton",
            allow_single_file = True,
            executable = True,
            cfg = "host",
        ),
    },
    executable = True,
)
\end{bazel}
    \item run and fail
    \item fix repository rule by adding a BUILD and WORKSPACE
\begin{bazel}
ctx.file("BUILD", """
exports_files(glob(["**/*"]))
""")
ctx.file("WORKSPACE", "workspace(name = \"{name}\")".format(name = ctx.name))
\end{bazel}
    \item run and fail again
    \item run with --sandbox\_debug
    \item open working directory and see that no library exists
    \item export library from repository
\begin{bazel}
filegroup(
    name = "library",
    srcs = glob(["lib/**/*"]),
    visibility = ["//visibility:public"],
)    
\end{bazel}
    \item include in rule
\begin{bazel}
"_compiler_library": attr.label(
    default = "@ml_compiler//:library",
    allow_files = True,
    cfg = "host",
)
\end{bazel}
    \item include in inputs
\begin{bazel}
inputs = depset(
    direct = ctx.files.main + ctx.files.srcs + ctx.files._compiler_library,
),
\end{bazel}
    \item run and yay
\end{enumerate}
\end{document}